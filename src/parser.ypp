%code requires {
#include <stdio.h>
#include "parseutils.h"
#include "parser.h"
#include "ast.h"

extern void yyerror(const char * const error);
}

%union {
    int int_val;
    float float_val;
    char * c_str;

    ASTNode * ast;
    Expr * expr;
    ExprList * expr_list;
    NumLiteral * num_literal;
    IntLiteral * int_literal;
    FloatLiteral * float_literal;
    BoolLiteral * bool_literal;
    Ident * ident;
    MathExpr * math_expr;
    BoolExpr * bool_expr;
    Typename * type_name;
    PtrTypename * ptr_type_name;
    MutTypename * mut_type_name;
    TupleTypename * tuple_type_name;
    ParensTypename * parens_type_name;
    TypenameList * type_list;
    VarDeclList * var_decl_list;
    FuncTypename * func_type_name;
    TypeAlias * type_alias;
    TypeDecl * type_decl;
    StructDecl * struct_decl;
    VarDecl * var_decl;
    VarDeclInit * var_decl_init;
    AddrOf * addr_of;
    Deref * deref;
    CastExpr * cast_expr;
    TupleExpr * tuple_expr;
}

%token <int_literal> INT
%token <float_literal> FLOAT
%token <bool_literal> BOOL
%token <ident> INT_TYPE FLOAT_TYPE CHAR_TYPE IDENT
%token FUNC_TYPE_OP NEWLINE

%right '='
%right TYPE_ALIAS_KW STRUCT_KW
%precedence FUNC_PREC
%right '('
%left ')'
%right BOOL_AND BOOL_OR
%right '+' '-'
%nonassoc CAST_KW
%right MUT
%left '*'
%right '&'

%type <num_literal> num
%type <expr> expr
%type <expr_list> expr_list
%type <math_expr> math_expr
%type <bool_expr> bool_expr
%type <type_name> type_name
%type <ident> simple_type_name
%type <ptr_type_name> ptr_type_name
%type <mut_type_name> mut_type_name
%type <tuple_type_name> tuple_type_name
%type <parens_type_name> parens_type_name
%type <func_type_name> func_type_name
%type <type_list> type_list
%type <var_decl_list> var_decl_list
%type <struct_decl> struct_decl
%type <type_alias> type_alias
%type <type_decl> type_decl
%type <var_decl> var_decl
%type <var_decl_init> var_decl_init
%type <addr_of> addr_of_expr
%type <deref> deref_expr
%type <cast_expr> cast_expr
%type <tuple_expr> tuple_expr

%%

statement : type_decl ';' {x::top = $1; YYACCEPT;}
          | var_decl ';' {x::top = $1; YYACCEPT;}
          | var_decl_init ';' {x::top = $1; YYACCEPT;}
          ;

expr : '(' expr ')' {$$ = new ParensExpr($2);}
     | math_expr {$$ = $1;}
     | bool_expr {$$ = $1;}
     | addr_of_expr {$$ = $1;}
     | deref_expr {$$ = $1;}
     | cast_expr {$$ = $1;}
     | tuple_expr {$$ = $1;}
     | BOOL {$$ = $1;}
     | num {$$ = $1;}
     | IDENT {$$ = $1;}
     ;

expr_list : expr {$$ = new ExprList({$1});}
          | expr_list ',' expr {$1->push_expr($3); $$ = $1;}
          ;

math_expr : expr '+' expr {$$ = new MathExpr('+', $1, $3);}
          | expr '-' expr {$$ = new MathExpr('-', $1, $3);}
          ;

bool_expr : expr BOOL_AND expr {$$ = new BoolExpr("&&", $1, $3);}
          | expr BOOL_OR expr {$$ = new BoolExpr("||", $1, $3);}
          ;

addr_of_expr : '&' expr {$$ = new AddrOf($2);}
             ;

deref_expr : '*' expr {$$ = new Deref($2);}
           ;

cast_expr : expr CAST_KW type_name {$$ = new CastExpr($3, $1);}
          ;

tuple_expr : '[' expr_list ']' {$$ = new TupleExpr($2);}
           ;

type_decl : type_alias {$$ = $1;}
          | struct_decl {$$ = $1;}
          ;

var_decl_init : var_decl '=' expr {$$ = new VarDeclInit($1, $3);}
              ;

var_decl : type_name IDENT {$$ = new VarDecl($1, $2);}
         ;

var_decl_list : var_decl ';' {$$ = new VarDeclList({$1});}
              | var_decl_list var_decl ';' {$1->push_decl($2); $$ = $1;}
              ;

struct_decl : STRUCT_KW IDENT '{' var_decl_list '}' {$$ = new StructDecl($2, $4);}
            ;

type_name : simple_type_name {$$ = $1;}
          | ptr_type_name {$$ = $1;}
          | mut_type_name {$$ = $1;}
          | tuple_type_name {$$ = $1;}
          | func_type_name {$$ = $1;}
          | parens_type_name {$$ = $1;}
          ;

func_type_name : '[' type_list ']' FUNC_TYPE_OP type_name %prec FUNC_PREC {$$ = new FuncTypename($2, $5);}
               ;

parens_type_name : '(' type_name ')' {$$ = new ParensTypename($2);}
                 ;

simple_type_name : INT_TYPE {$$ = $1;}
                 | FLOAT_TYPE {$$ = $1;}
                 | CHAR_TYPE {$$ = $1;}
                 | IDENT {$$ = $1;}
                 ;

ptr_type_name : type_name '*' {$$ = new PtrTypename($1);}
              ;

mut_type_name : MUT type_name {$$ = new MutTypename($2);}
              ;

tuple_type_name : '[' type_list ']' {$$ = new TupleTypename($2);}
                ;

type_list : type_name {$$ = new TypenameList({$1});}
          | type_list ',' type_name {$1->push_type($3); $$ = $1;}
          ;

type_alias : TYPE_ALIAS_KW IDENT '=' type_name {$$ = new TypeAlias($2, $4);}
           ;

num : INT {$$ = $1;}
    | FLOAT {$$ = $1;}
    | '-' INT {
            $$ = new IntLiteral(-$2->value);
            delete $2;
        }
    | '-' FLOAT {
            $$ = new FloatLiteral(-$2->value);
            delete $2;
        }
    ;
